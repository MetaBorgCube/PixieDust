module pixiedust/View

imports
  Common
  Expressions
  Types
  pixiedust/Action
  pixiedust/Types
  
context-free syntax
  
  View = Component
  
  Component.Component = <
    component <ID>(<{TypeParameter ", "}*>){
      <{ViewMember "\n"}*>
    }
  >
  
  ViewMember.Render = Exp
  ViewMember = ActionDeclaration
  
  Render = RenderLoop
  Render = RenderComponent
  Render = RenderDomElement 
  
  RenderDomElement.RenderDomElement  = <
    <DomID>[<{DomElementProperty ", "}*>] {
      <{Exp "\n"}*>
    }> 
  RenderDomElement.RenderDomElement2 = <
    <DomID> {
      <{Exp "\n"}*>
    }>
  RenderDomElement.RenderDomElement3 = <<DomID>[<{DomElementProperty ", "}*>]>
  
  RenderComponent.RenderComponent  = <
    @<ComponentRef>(<{Exp ", "}*>) { 
      <{Exp "\n"}*> 
    }>
  RenderComponent.RenderComponent2 =  <
    @<ComponentRef> { 
      <{Exp "\n"}*> 
    }>
  RenderComponent.RenderComponent3 = <@<ComponentRef>(<{Exp ", "}*>)> { prefer } //above Rendercomponent4 ( Exp )
  
  
  RenderComponent = RenderComponent4
  RenderComponent4.RenderComponent4 = <@<ComponentRef>> //{ avoid }
  
  
  ComponentRef.ComponentRef = ID
  ComponentRef.ComponentRefFromImport = <<ID>.<ID>>
  
  RenderLoop.RenderLoop = <
    for(<ID> in <Exp>) <Exp>
  >
  
  DomElementProperty.DomElementProperty = <<ID> = <DomElementPropertyValue>>
  
  DomElementPropertyValue.DomElementPropertyValueExp = Exp
  DomElementPropertyValue.DomElementPropertyValueMap = <{
    <{DomElementProperty ", "}*>
  }>
  DomElementPropertyValue = Action
  
  context-free restrictions
  
  RenderComponent4 -/- [\(]
    
//    Render.RenderExp > Render.RenderComponent

lexical syntax
  DomID = ID
  DomID = Keyword { reject }
  DomID = ViewKeyword { reject }
  
  ViewKeyword = "children"  
