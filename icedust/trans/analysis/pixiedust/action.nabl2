module pixiedust/action

imports
  pixiedust/view

rules

  [[ ActionCall(name, args) ^ (s) ]] := 
    Member{name} -> s,
    Member{name} |-> action_def,
    action_def : TTuple(a_ty, TOne(), a_mul),
    a_ty == TAction(a_args),
    PairwiseEqual [[ args ^ (s, a_args) ]].
    
  
  [[ ActionDeclaration(name, params, stmts) ^ (module_scope) ]] :=
    new body_scope,
    body_scope -P-> module_scope,
    Map1T [[ params ^ (body_scope) : params_ty ]],
    Map1 [[ stmts ^ (body_scope)]],
    Member{name} <- module_scope,
    Member{name} : TTuple(TAction(params_ty), TOne(), TOnDemandIncremental()),
    Member{name}.side := None().
  
  [[ CreateEntityStatement(stmt) ^ (s) ]] := [[ stmt ^ (s, s) : _ ]].
  [[ UpdateEntityStatement(UpdateEntity(e, fields)) ^ (s) ]] := 
    [[ e ^ (s) : e_tpl ]],
    e_tpl == TTuple(e_type, e_mult, e_strat),
    e_type == TEntity(e_type_entity),
    
    new update_scope,
    update_scope -P-> s,
    Implicit{"this"} <- update_scope,
    Implicit{"this"} : e_tpl,
    Map2 [[ fields ^ (s, update_scope) ]].
  
  [[ CreateEntityInstance(var, type, fields) ^ (body, s) : var_tpl ]] := 
    [[ type ^ (s) : entity_type ]],
    entity_type == TEntity(entity),
    entity ?=I=> entity_scope,
    
    var_tpl == TTuple(entity_type, TOne(), TOnDemandIncremental()),
    Member{var} <- s,
    Member{var} : var_tpl,
    Member{var}.side := None(),
    
    new instance_scope,
    instance_scope -P-> s,
    instance_scope -I-> entity_scope,
    
    Implicit{"this"} <- instance_scope,
    Implicit{"this"} : var_tpl,
    
    Map2 [[ fields ^ (body, instance_scope) ]].
    
  [[ c @ CreateEntityInstance(var, type, fields) ^ (s) ]] := [[c ^ (s) : _]].
    
  [[ UpdateEntity(e, fields) ^ (body, s) ]] := 
    Implicit{"this"} -> s,
    Implicit{"this"} |-> this_def,
    this_def : TTuple(this_type, this_mult, this_strat),
    this_type == TEntity(this_type_entity),
    this_type_entity ?=I=> entity_scope,
    
    new update_exp_scope,
    update_exp_scope -P-> s,
    update_exp_scope -J-> entity_scope,
    
    [[ e ^ (update_exp_scope) : e_tpl ]],
    e_tpl == TTuple(e_type, e_mult, e_strat),
    e_type == TEntity(e_type_entity),
    
    new update_scope,
    update_scope -P-> s,
    Implicit{"this"} <- update_scope,
    Implicit{"this"} : e_tpl,
    Map2 [[ fields ^ (body, update_scope) ]].
    
  [[ EntityMember(m, value) ^ (body, s) ]] :=
    Implicit{"this"} -> s,
    Implicit{"this"} |-> this_def,
    this_def : this_tpl,
    this_tpl == TTuple(this_type, this_m, this_strat),
    this_type == TEntity(this_type_entity), 
    this_type_entity ?=I=> entity_scope,
    new member_key_scope,
    member_key_scope -I-> entity_scope,
    Member{m} -> member_key_scope,
    Member{m} |-> member_def,
    member_def : m_tpl,
    m_tpl == TTuple(m_type, m_mult, m_strat),
    
    new member_value_scope,
    member_value_scope -P-> body,
    member_value_scope -X-> entity_scope,
    Implicit{"this"} <- member_value_scope,
    Implicit{"this"} : this_tpl,
    [[ value ^ (member_value_scope) : value_tpl ]],
    value_tpl == TTuple(value_type, value_mult, value_strat),
    value_type  <sub?    m_type  | error $[Type error: expected [m_type] got [value_type].]@value,
    value_mult  <mulOrd? m_mult  | error $[Multiplicity error: expected [m_mult] got [value_mult].]@value,
    expected_strat == TOnDemandIncremental(),
    value_strat <strat? expected_strat | error $[Calculation strategy error: expected [expected_strat] got [value_strat].]@value.
    
    
    
//    EntityMemberValue [[ value ^ (body, this_tpl, m_tpl) ]].
//  EntityMemberValue [[ value ^ (body, this_tpl, m_tpl) ]] :=
    
    