module api/component-api

imports
  lib/nabl2

  
  analysis/_constructors

  api/expressions-api
  api/types-names-api
  api/types-api
  
  signatures/pixiedust/View-sig
  signatures/pixiedust/Types-sig

rules
  modulename-get-componentnames = get-decl-assoc(|"Module") ; get-scope-decl-names(|"Component")

  componentname-get-component-kind = get-property(|"Component","componentKind")
  componentname-is-normal-kind = where(componentname-get-component-kind ; ?NormalComponentKind())
  componentname-is-native-kind = where(componentname-get-component-kind ; ?NativeComponentKind())
  
  componentname-get-type = get-type-property(|"Component") ; normalize-type
  
  componentname-get-parameters = get-property(|"Component","parameters")
  componentname-get-body = get-property(|"Component","body")
  
  componentname-get-parameter-names = map(componentparameter-get-name)
    
  componentname-get-signature = componentname-get-parameter-names ; map(componentparameter-get-properties)
  componentname-get-signature-pp = componentname-get-signature ; map(componentparameter-properties-pp)
  
  componentparameter-get-name = ?TypeParameter(<id>, _, _)
  componentparameter-get-name = ?RefTypeParameter(<id>, _, _)
  
  componentparameter-get-type = get-type-property(|"Member") ; normalize-type
  componentparameter-get-multiplicity = get-type-property(|"Member") ; normalize-multiplicity
  
  componentparameter-get-variable-type = get-property(|"Member","variableType")
  
  componentparameter-is-normal = where(componentparameter-get-variable-type ; ?NormalVariableType())
  componentparameter-is-boxed = where(componentparameter-get-variable-type ; ?BoxedVariableType())
  
  componentname-collect-refs = 
    where(componentname-is-normal-kind) ; componentname-get-body ; collect(is-ref)
    
  
  componentparameter-get-properties = !(
    <id>,
    <componentparameter-get-type>,
    <componentparameter-get-multiplicity>,
    <componentparameter-get-variable-type>
  )
  componentparameter-properties-pp: (name, type, m, vtype) -> pp
    with
      ppType := <pp2-type-multiplicity> (type, m)
    ; ppVType := <?BoxedVariableType() < !"ref " + !""> vtype
    ; pp := $[[ppVType][name] : [ppType]]
  
  
  