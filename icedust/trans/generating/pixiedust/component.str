module generating/pixiedust/component

imports
  signatures/pixiedust/View-sig
  signatures/pixiedust/Types-sig
  
  generating/pixiedust/expression
  generating/pixiedust/names

  api/component-api
  api/types-names-api
  api/action-api
  
  js/js-util

rules

  generate-components: moduleName -> stmts
    with
      x_componentScope := <component-scope-var>
    ; components := <modulename-get-componentnames ; filter(componentname-is-normal-kind)> moduleName
    ; exp_component* := <map(componentname-to-js-exp)> components
    ; prop_component* := <zip ; map(component-to-prop)> (components, exp_component*)
    ; stmts := js-stmt* |[
        var x_componentScope = (function(){
          var oldScope = scope;  
          return {
            prop_component*
          };
        })();
        module.exports.x_componentScope = x_componentScope;
      ]|
      
  componentname-to-js-exp : x_componentName -> result
    where
      <componentname-is-normal-kind> x_componentName
    with
      body := <componentname-get-body> x_componentName
    ; renderExpressions := <componentbody-get-render-expressions> body 
    ; (exp_body*, stmts_stmts_body, i') := <exp-fold(|0)> renderExpressions
    ; stmts_body := <concat> stmts_stmts_body
    ; exp_wrapped := <component-wrap-elements> (exp_body*, renderExpressions)
    ; exp_actionName* := <componentname-get-actionnames ; map(js-string)> x_componentName
    ; result := js-exp |[
        Lifted(
          function x_componentName(props, state){
            var scope = _.assign({}, oldScope, props);
            stmts_body
            return {
              result: exp_wrapped,
              state: state
            };
          }, 
          [exp_actionName*]
        )
    ]|
    
  component-wrap-elements: ([], []) -> <js-null>
  component-wrap-elements: ([exp_element|[]], [e_element|[]]) -> exp_element
    where
      View() := <get-type2> e_element
  component-wrap-elements: (exp_element*, _) -> js-exp |[
    React.createElement('group', {}, exp_element*)
  ]|
  
  component-to-prop: (x_componentname, exp_component) -> js-prop |[
    x_componentname: exp_component
  ]|