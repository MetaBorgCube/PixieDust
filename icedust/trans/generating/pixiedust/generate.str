module generating/pixiedust/generate

imports
  api/model-names-api

  generating/pixiedust/model
  generating/pixiedust/data
  generating/pixiedust/execute
  
  lib/scopes
  lib/js/util
  
rules

  generate-pixiedust-code: x_moduleName -> stmts_pixiedust
    with
      stmts_imports := <generate-imports>  
    ; (env, [
        stmts_model,
        stmts_data,
        stmts_execute
      ]) := <thread-environment(
        env-thread-param(env-fold-mapconcat(entityname-to-js-stmts) | <modulename-get-entitynames> x_moduleName),
        push-scope,
        env-thread-param(modulename-to-data-stmts | x_moduleName),
        env-thread-param(modulename-to-execute-stmts | x_moduleName),
        pop-scope
      )> <new-environment>
    ; stmt_execution := <conc ; js-lexical-scope-stmt> (stmts_data, stmts_execute)
    ; stmts_pixiedust := js-stmts |[
      stmts_imports  
      stmts_model
      stmt_execution
    ]| 
  
  generate-imports = !js-stmts |[
    var functions = require("./expressions");
    var _ = require("lodash");
    var React = require("react");
    var ReactDOMServer = require("react-dom/server");
    
    var runtime = {
      callDirtySubscriber: function callDirtySubscriber(dirtySubscriber) {
        dirtySubscriber();
      }
    };
  ]|