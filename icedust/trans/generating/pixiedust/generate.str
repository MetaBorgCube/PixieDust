module generating/pixiedust/generate

imports
  api/module-names-api
  api/model-names-api

  generating/pixiedust/model
  generating/pixiedust/data
  generating/pixiedust/execute
  generating/pixiedust/view
  
  lib/scopes
  lib/js/util
  
rules

  generate-pixiedust-code: x_moduleName -> stmts_pixiedust
    with
      stmts_imports := <generate-imports>  x_moduleName
    ; (env, [
        stmts_model,
        stmts_execute,
        stmts_view
      ]) := <thread-environment(
        env-thread-param(env-fold-mapconcat(entityname-to-js-stmts) | <modulename-get-entitynames> x_moduleName),
        env-thread-param(modulename-to-execute-stmts | x_moduleName),
        env-thread-param(modulename-to-view-stmts | x_moduleName)
      )> <new-environment>
    ; stmts_pixiedust := js-stmts |[
      stmts_imports  
      stmts_model
      stmts_view
      stmts_execute
    ]| 
    
  
  generate-imports: x_moduleName -> result
    with 
      prop_import* := <modulename-get-importpaths ; map(generate-import-prop)> x_moduleName
    ; result := js-stmts |[
        var runtime = require("./src/runtime/runtime");
        var functions = require("./src/runtime/expression");
        var PixieDustComponent = require("./src/runtime/components/PixieDustComponent");
        var _ = require("lodash");
        var React = require("react");
        var ReactDOMServer = require("react-dom/server");
        var imports = {
          prop_import*
        };
      ]|
  
  generate-import-prop: x_importPath -> js-prop |[
    exp_importPath: require(exp_importPath)
  ]|
    with
      exp_importPath := <js-string> x_importPath
      